#+TILTE:LiquidZulu's Doom Config
/This file is best viewed in [[https://www.gnu.org/software/emacs/][emacs]]!/

* Introduction
This is my config for [[https://github.com/hlissner/doom-emacs][Doom Emacs]]
#+begin_quote
...a configuration framework for GNU Emacs tailored for Emacs bankruptcy veterans who want less framework in their frameworks, a modicum of stability (and reproducibility) from their package manager, and the performance of a hand rolled config (or better). It can be a foundation for your own config or a resource for Emacs enthusiasts to learn more about our favorite operating system.
#+end_quote
** Modifying this config
=config.org= contains a number of source blocks that may be modified at will, =config.el= will automatically load all elisp source contained in =README.org=.
** Why Doom?
You will notice from a perusing of =init.el= that I have disabled [[https://www.emacswiki.org/emacs/Evil][Evil Mode]], so I am clearly not on Doom for the vi bindings. I use Doom for the speed and to avoid bankruptcy --- vanilla bindings are good enough in most areas though I have modified the movement to make sense on Colemak-DHm.

* Lexical Binding
Enable Lexical Binding
#+begin_src elisp
;;; $DOOMDIR/config.el -*- lexical-binding: t; -*-
#+end_src

* Helper Functions
I got these from [[https://dustinlacewell.github.io/emacs.d/][Dustin Lacewell's init.el]]. I dont use most of these but its nice to have them if im going to be taxing things from his init.

** :function
Make non-interactive functions out of forms or a symbol.
#+begin_src elisp
(defmacro :function (&rest body)
  (if (->> body length (< 1))
      `(lambda () ,@body)
    (pcase (car body)
      ;; command symbol
      ((and v (pred commandp))
       `(lambda () (call-interactively (quote ,v))))
      ;; function symbol
      ((and v (pred symbolp))
       `(lambda () (,v)))
      ;; quoted command symbol
      ((and v (pred consp) (guard (eq 'quote (car v))) (pred commandp (cadr v)))
       `(lambda () (call-interactively ,v)))
      ;; quoted function symbol
      ((and v (pred consp) (guard (eq 'quote (car v))))
       `(lambda () (,(cadr v))))
      ;; body forms
      (_ `(lambda () ,@body) ))))
#+end_src

** :command
Make interactive commands out of forms or a symbol.
#+begin_src elisp
(defmacro :command (&rest body)
  (if (->> body length (< 1))
      `(lambda () (interactive) ,@body)
    (pcase (car body)
      ;; command symbol
      ((and v (pred commandp))
       `(lambda () (interactive) (call-interactively (quote ,v))))
      ;; function symbol
      ((and v (pred symbolp))
       `(lambda () (interactive) (,v)))
      ;; quoted command symbol
      ((and v (pred consp) (guard (eq 'quote (car v))) (pred commandp (cadr v)))
       `(lambda () (interactive) (call-interactively ,v)))
      ;; quoted function symbol
      ((and v (pred consp) (guard (eq 'quote (car v))))
       `(lambda () (interactive) (,(cadr v))))
      ;; body forms
      (_ `(lambda () (interactive) ,@body) ))))
#+end_src

** :after
Defer some forms until the given package is loaded.
#+begin_src elisp
(defmacro :after (package &rest body)
  "A simple wrapper around `with-eval-after-load'."
  (declare (indent defun))
  `(with-eval-after-load ',package ,@body))
#+end_src

** :hook
Register some forms or a symbol with a hook.
#+begin_src elisp
(defmacro :hook (hook-name &rest body)
  "A simple wrapper around `add-hook'"
  (declare (indent defun))
  (let* ((hook-name (format "%s-hook" (symbol-name hook-name)))
         (hook-sym (intern hook-name))
         (first (car body))
         (local (eq :local first))
         (body (if local (cdr body) body))
         (first (car body))
         (body (if (consp first)
                   (if (eq (car first) 'quote)
                       first
                     `(lambda () ,@body))
                 `',first)))
    `(add-hook ',hook-sym ,body nil ,local)))
#+end_src

** :push
A wrapper around add-to-list.
#+begin_src elisp
(defmacro :push (sym &rest body)
  (declare (indent defun))
  (if (consp body)
      `(setq ,sym (-snoc ,sym ,@body))
    `(add-to-list ,sym ,body)))
#+end_src

** :bind
Bind some forms or a symbol to a key. (I use map! instead)
#+begin_src elisp
(defmacro :bind (key &rest body)
  (declare (indent defun))
  (pcase key
    ;; kbd string resolving symbol
    ((and k (pred symbolp) (pred boundp) (guard (stringp (eval key))))
     `(global-set-key (kbd ,(eval key)) ,(eval `(:command ,@body))))
    ;; partial mode symbol
    ((pred symbolp)
     (let ((mode (intern (format "%s-map" key)))
           (key (eval (car body)))
           (body (eval `(:command ,@(cdr body)))))
       `(define-key ,mode (kbd ,key) ,body)))
    ;; global binding
    (_ `(global-set-key (kbd ,key) ,(eval `(:command ,@body))))))
#+end_src

* Settings

** email
Email and username, some functionality uses this to identify you, e.g. GPG configuration, email clients, file templates and snippets.
#+begin_src elisp
(setq user-full-name "LiquidZulu"
      user-mail-address "liquidzulu@pm.me")
#+end_src

** org-directory
I don't want all of my orgfiles on my c drive, I like to keep them here
#+begin_src elisp
(setq org-directory "e:/emacs/documents/notes/org")
#+end_src

** line number style
This determines the style of line numbers in effect. If set to =nil=, line numbers are disabled. For relative line numbers, set this to =relative=.
#+begin_src elisp
(setq display-line-numbers-type t)
#+end_src

** various misc setings
Blah
#+begin_src elisp
(setq-default
    delete-by-moving-to-trash t            ; Delete files to trash
    tab-width 4                            ; Set width for tabs
    uniquify-buffer-name-style 'forward    ; Uniquify buffer names
    window-combination-resize t            ; take new window space from all other windows (not just current)
    x-stretch-cursor t)                    ; Stretch cursor to the glyph width

(setq undo-limit 80000000                  ; Raise undo-limit to 80Mb
    evil-want-fine-undo t                  ; By default while in insert all changes are one big blob. Be more granular
    auto-save-default t                    ; Nobody likes to loose work, I certainly don't
    inhibit-compacting-font-caches t       ; When there are lots of glyphs, keep them in memory
    truncate-string-ellipsis "â€¦")          ; Unicode ellispis are nicer than "...", and also save /precious/ space

(delete-selection-mode 1)                  ; Replace selection when inserting text
(setq line-spacing 0.3)                    ; seems like a nice line spacing balance.
#+end_src

** Autosaves
#+begin_src elisp
(setq
 auto-save-default t
 auto-save-timeout 20   ; every 20 secs
 auto-save-interval 20) ; or every 20 keystrokes
#+end_src

** Prettify symbols
#+begin_src elisp
(global-prettify-symbols-mode 1)
#+end_src

** Parenthesis Settings
*** Rainbow Delimiters
Took this from ldle's init, found [[https://dustinlacewell.github.io/emacs.d/][here]].
#+begin_quote
Colour parenthesis based on their depth, using the golden ratio (because why not).
#+end_quote
#+begin_src elisp
(require 'color)
(defun gen-col-list (length s v &optional hval)
  (cl-flet ( (random-float () (/ (random 10000000000) 10000000000.0))
          (mod-float (f) (- f (ffloor f))) )
    (unless hval
      (setq hval (random-float)))
    (let ((golden-ratio-conjugate (/ (- (sqrt 5) 1) 2))
          (h hval)
          (current length)
          (ret-list '()))
      (while (> current 0)
        (setq ret-list
              (append ret-list
                      (list (apply 'color-rgb-to-hex (color-hsl-to-rgb h s v)))))
        (setq h (mod-float (+ h golden-ratio-conjugate)))
        (setq current (- current 1)))
      ret-list)))

(defun set-random-rainbow-colors (s l &optional h)
  ;; Output into message buffer in case you get a scheme you REALLY like.
  ;; (message "set-random-rainbow-colors %s" (list s l h))
  (interactive)
  (rainbow-delimiters-mode t)

  ;; Show mismatched braces in bright red.
  (set-face-background 'rainbow-delimiters-unmatched-face "red")

  ;; Rainbow delimiters based on golden ratio
  (let ( (colors (gen-col-list 9 s l h))
         (i 1) )
    (let ( (length (length colors)) )
      ;;(message (concat "i " (number-to-string i) " length " (number-to-string length)))
      (while (<= i length)
        (let ( (rainbow-var-name (concat "rainbow-delimiters-depth-" (number-to-string i) "-face"))
               (col (nth i colors)) )
          ;; (message (concat rainbow-var-name " => " col))
          (set-face-foreground (intern rainbow-var-name) col))
        (setq i (+ i 1))))))

(use-package rainbow-delimiters :commands rainbow-delimiters-mode :hook ...
  :init
  (setq rainbow-delimiters-max-face-count 16)
  (set-random-rainbow-colors 0.6 0.7 0.5)
  (:hook prog-mode 'rainbow-delimiters-mode))
#+end_src

*** Highlight Matching Bracket
#+begin_src elisp
(require 'paren)
(show-paren-mode 1)
(setq show-paren-delay 0)
(:after xresources
  (set-face-foreground 'show-paren-match (theme-color 'green))
  (set-face-foreground 'show-paren-mismatch "#f00")
  (set-face-attribute 'show-paren-match nil :weight 'extra-bold)
  (set-face-attribute 'show-paren-mismatch nil :weight 'extra-bold))
#+end_src

* Key Mapping
I use [[https://colemakmods.github.io/mod-dh/][Colemak-DHm]] -- my specific layout can be found [[https://configure.ergodox-ez.com/ergodox-ez/layouts/BO06w/latest/0][here]] -- so remapping of certain core keys, namely motion keys, is required for ergonomics and ease of use.
#+begin_src elisp
(map!
    "C-l"   #'beginning-of-line
    "C-u"   #'end-of-line
    "C-n"   #'backward-char
    "C-e"   #'forward-char

    "M-l"   #'previous-line
    "M-u"   #'next-line
    "M-n"   #'backward-word
    "M-e"   #'forward-word

    "M-C-l" #'(lambda () (interactive) (previous-line) (beginning-of-line))
    "C-M-u" #'(lambda () (interactive) (next-line)     (end-of-line))
    "C-M-n" #'backward-paragraph
    "C-M-e" #'forward-paragraph

    ;; "C-M-x f a"   #'helm-bibtex         ; "find article" : opens up helm bibtex for search.
    ;; "C-M-x o n"   #'org-noter           ; "org noter"  : opens up org noter in a headline
    ;; "C-M-x r c i" #'org-clock-in        ; "routine clock in" : clock in to a habit.
    ;; "C-M-x c b"   #'beacon-blink        ; "cursor blink" : makes the beacon-blink
    )
#+end_src

* Maximising Frame on Windows
Pretty sure this is done by
#+begin_src elisp
(if (eq initial-window-system 'x)       ; if started by emacs command or desktop file
    (toggle-frame-maximized))
#+end_src
on linux but adding this
#+begin_src elisp
(defun maximize-frame ()
  "Maximizes the active frame in Windows"
  (interactive)
  ;; Send a `WM_SYSCOMMAND' message to the active frame with the
  ;; `SC_MAXIMIZE' parameter.
  (when (eq system-type 'windows-nt)
    (w32-send-sys-command 61488)))
(add-hook 'window-setup-hook 'maximize-frame t)
#+end_src
makes it work on win 10.

* Only Show Encoding When Not UTF-8
I basically only use UTF-8 so it takes up space for no reason most of the time.
#+begin_src elisp
(defun doom-modeline-conditional-buffer-encoding ()
  (setq-local doom-modeline-buffer-encoding
              (unless (or (eq buffer-file-coding-system 'utf-8-unix)
                          (eq buffer-file-coding-system 'utf-8)))))
(add-hook 'after-change-major-mode-hook #'doom-modeline-conditional-buffer-encoding)
#+end_src

* Add Default Preamble to New Orgmode Files
I dont like to type out all that crap on my own
#+begin_src elisp
(setq
 org-preamble (format
               "#+TITLE:\n#+AUTHOR:LiquidZulu\n#+BIBLIOGRAPHY:e:/Zotero/library.bib\n#+PANDOC_OPTIONS: csl:e:/Zotero/styles/australasian-physical-and-engineering-sciences-in-medicine.csl\n#+DATE:%s\n/This file is best viewed in [[https://www.gnu.org/software/emacs/][emacs]]!/"
               (current-time-string)))

(add-hook 'find-file-hook
          (lambda ()
            (if
                (string=
                 (substring
                  (buffer-name)
                  (if (> (length (buffer-name)) 3) (- (length (buffer-name)) 3) 0)
                  nil)
                 "org")
                (if
                    (=
                     (buffer-size)
                     0)
                    (insert org-preamble)))))
#+end_src

* Package Config
** Splashcii
Splashcii is a CLI program that generates random ASCII splashart, you need to download it [[https://github.com/folke/splashcii][here]] for this to work.
#+begin_src elisp
;; splashcii
(defvar +fl/splashcii-query ""
  "The query to search on asciiur.com")

(defun +fl/splashcii ()
  (split-string (with-output-to-string
                  (call-process "splashcii" nil standard-output nil +fl/splashcii-query))
                "\n" t))

(defun +fl/doom-banner ()
  (let ((point (point)))
    (mapc (lambda (line)
            (insert (propertize (+doom-dashboard--center +doom-dashboard--width line)
                                'face 'doom-dashboard-banner) " ")
            (insert "\n"))
          (+fl/splashcii))
    (insert (make-string (or (cdr +doom-dashboard-banner-padding) 0) ?\n))))

;; override the first doom dashboard function
(setcar (nthcdr 0 +doom-dashboard-functions) #'+fl/doom-banner)

;; (setq +fl/splashcii-query "space")
#+end_src
** org-ref
org-ref is necessary for writing of any sort of academic material in emacs, makes citations a lot easier.
#+begin_src elisp
(use-package! org-ref
    :after org
    :init
    ; code to run before loading org-ref
    :config
    ; code to run after loading org-ref
    )
(setq
      ;org-ref-notes-directory (concatenate 'string org-directory "/org-ref")
      org-ref-default-bibliography '("e:/Zotero/library.bib")
      org-ref-pdf-directory "e:/Zotero/pdfs")

(after! org
  (add-to-list 'org-capture-templates
               '(("a"               ; key
                  "Article"         ; name
                  entry             ; type
                  ;(file+headline (concatenate 'string org-directory "/foo.org) "Article")  ; target
                  "\* %^{Title} %(org-set-tags)  :article: \n:PROPERTIES:\n:Created: %U\n:Linked: %a\n:END:\n%i\nBrief description:\n%?"  ; template

                  :prepend t        ; properties
                  :empty-lines 1    ; properties
                  :created t        ; properties
))) )
#+end_src
** helm-bibtex
Helm BibTeX is a package that allows for searching through BibTeX bibliographies.
#+begin_src elisp
(use-package! helm-bibtex
  :after org
  :init
  ; blah blah
  :config
  ;blah blah
  )

(setq bibtex-format-citation-functions
      '((org-mode . (lambda (x) (insert (concat
                                         "\\cite{"
                                         (mapconcat 'identity x ",")
                                         "}")) ""))))
(setq
      bibtex-completion-pdf-field "file"
      bibtex-completion-bibliography
      '("e:/Zotero/library.bib")
      bibtex-completion-library-path '("e:/Zotero/")
     ; bibtex-completion-notes-path "~/Dropbox/Org/references/articles.org"  ;; not needed anymore as I take notes in org-roam
      )
#+end_src
